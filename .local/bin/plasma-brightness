#!/bin/fish

set levels 1 2 4 10 20 40 60 80 100

function levelup -a current
    for level in $levels
        if [ $level -gt $current ]
            echo $level
            return
        end
    end
    echo $current
end

function leveldown -a current
    for level in $levels[-1..1]
        if [ $level -lt $current ]
            echo $level
            return
        end
    end
    echo $current
end

function brightnessControl
    set command $argv[1]
    set -e argv[1]
    qdbus org.kde.Solid.PowerManagement \
        /org/kde/Solid/PowerManagement/Actions/BrightnessControl \
        "org.kde.Solid.PowerManagement.Actions.BrightnessControl.$command" $argv
end

function main
    set command $argv[1]
    set brightnessMax (brightnessControl brightnessMax)
    set current (math round (brightnessControl brightness) / $brightnessMax x 100)

    set new_level
    if [ "$command" = up ]
        set new_level (levelup $current)
    else if [ "$command" = down ]
        set new_level (leveldown $current)
    else
        exit 2
    end

    set brightness (math round $new_level x $brightnessMax / 100)
    brightnessControl setBrightness $brightness
end

main $argv
